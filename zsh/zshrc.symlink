# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# source oh-my-zsh first, so that we can override stuff later
source $DOTFILES/zsh/oh-my-zsh.zsh

# your project folder that we can `c [tab]` to
export PROJECTS=~/www

# run docker eval
eval "$(docker-machine env default)"

# use ~/.secret for setting up sensitive stuff that 
# shouldn't be in your public repo
if [[ -a ~/.secret ]]
then
  source ~/.secret
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but oh-my-zsh, the path and completion files
for file in ${${${config_files:#*/oh-my-zsh.zsh}:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
